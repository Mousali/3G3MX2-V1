#!/usr/bin/env sh
/*** 2>/dev/null
DOCKER_IMAGE=swipl:9.3.23
DOCKER_CMD="/usr/bin/swipl -q"
DOCKER_EXTRA_ARGS="-w $(pwd) -u $(id -u):$(id -g) -v $(pwd):$(pwd):rw"
s="$(cd "$(dirname "$0")" && pwd)/$(basename "$0")";docker run --rm -a stdin -a stdout -a stderr -i$([ -t 0 ] && echo t) --init -v "$s":"$s":ro ${DOCKER_EXTRA_ARGS} ${DOCKER_IMAGE} ${DOCKER_CMD} "$s" "$@";exit $?
***/

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% vfdparam  -   Calculate VFD parameter values given a VFD and motor 
%               specification YAML file. 
%  
% USAGE:
%   vfdparam.pl [OPTIONS] [<spec_file> ...]
%
% OPTIONS:
%   --calculated-only  -c  boolean=false    Print calculated parameter values only
%   --output-file      -o  FILE:atom=false  write output to FILE
%   --help             -h  boolean=false    Show this help message
%
% INPUT:
%   Specification YAML string from stdin or a list of specification YAML file names.
%
% OUTPUT:
%   Prints VDF parameter values to stdout or output file.
%
% EXIT STATUS:
%   - 0: Success
%   - 1: File error or invalid YAML
%
% AUTHOR:
%   Ammar Mousali
%
% LICENSE:
%   GNU AFFERO GENERAL PUBLIC LICENSE
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

:- use_module(library(readutil)).
:- use_module(library(optparse)).

:- initialization(main, main).

read_specs([], Str):-
    read_string(user_input,_,Str).
read_specs([H|[]], Str):- 
    read_file_to_string(H, Str, []).
read_specs([H|T], Str):- 
    read_file_to_string(H, StrH, []),
    read_specs(T, StrT),
    string_concat(StrH, "\n\n", StrHH),
    string_concat(StrHH, StrT, Str).

% loading predicates for VFD parameters for a specific inverter model
load_param_def(Manufacturer ,Model):-
    string_lower(Manufacturer, "omron"),
    re_match("3G3MX2-.*-V1"/i, Model),
    consult("omron/3g3mx2-v1/3g3mx2-v1.pl").

main(Args):-
    OptsSpec=[
        [opt(calculated_only), type(boolean), default(false),shortflags([c]), longflags(['calculated-only']),help('Print calculated parameter values only')], 
        [opt(help), type(boolean), shortflags([h]), longflags([help]), help('Show this help message'), default(false)],
        [opt(outfile), meta('FILE'), type(atom), shortflags([o]), longflags(['output-file']),help('write output to FILE'), default(std_out)]
    ],
    opt_parse( OptsSpec, Args, Ops, PositionalArgs),

    (
        Ops.help == true ->
        (
            opt_help(OptsSpec, Help),
            format('USAGE:~n~+vfdparam [OPTIONS] [<spec_file> ...]~n~nCalculate Omron VFD configuration parameters from YAML VFD and motor specification file.~n~nOPTIONS:~n~w~n', [Help]),
            halt(0)
        )
    ; true
    ),
    
    % read the specification YAML file(s), if no files, read from stdin
    read_specs(PositionalArgs, Str),
    yaml_read(string(Str), Yaml_spec),
    b_setval(spec, Yaml_spec),

    % load the parameter definitions for the specified inverter
    load_param_def(Yaml_spec.get(inverter/brand), Yaml_spec.get(inverter/model)),

    (
        Ops.outfile \= std_out ->  
        (
            open(Ops.outfile, write, Out),
            set_output(Out)
        )
    ; true
    ),

    findall(Predicate, 
        ( 
            current_predicate(Predicate/2),
            is_vfd_param(Predicate)
        ), Params),
    sort(Params, UniqueParams),

    findall([Param, Value], 
        (
            member(Param, UniqueParams), 
            call(Param,Value, Msg),
            (
                Ops.calculated_only ->
                Msg \= default 
            ; true
            ),
            string_upper(Param,Uppercase_Param), 
            format('~w: ~|~` t~w~6+ # ~w~n', [Uppercase_Param,Value, Msg])
        ), _
    ),

    halt(0).